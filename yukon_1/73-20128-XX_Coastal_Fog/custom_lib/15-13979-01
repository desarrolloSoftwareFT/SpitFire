!!!!    6    0    1 1152545857  V2139                                         

!-----------------------------------------------------------------------
! Device:        adt7411
! Manufacturer:  Analog Devices
! Description:   SPI/I2C Compatible 10-Bit Digital Temperture Sensor and
!                8-Channel ADC
! Package:       16 pin QSOP
! Test Platform: 3065,3070
! Safeguard:     standard_cmos
! Written By    : mikef
! Date Written  : 7/10/06
! Verified      : No
!
!-----------------------------------------------------------------------
!
! Additional Comments:
!
!-----------------------------------------------------------------------
vector cycle 10u
receive delay 9u
!

assign VCC     to pins 6
assign GND     to pins 5

assign SCL     to pins 13
assign SDA     to pins 12

assign AIN8    to pins 15      !Analog input
assign AIN7    to pins 16      !Analog input
assign AIN6    to pins 1       !Analog input
assign AIN5    to pins 2       !Analog input
assign AIN4    to pins 14      !Analog input
assign AIN3    to pins 9       !Analog input
assign D_minus_AIN2 to pins 8  !Analog input
assign D_plus_AIN1 to pins 7   !Analog input

assign NC      to pins 3       !No connect
assign CS_bar  to pins 4  default "1"  !Chip Select for SPI interface

assign Int_Int_bar to pins 10  !Output - open source - Over temp limit
assign Dout    to pins 11      !Output - SPI interface data out pin



family TTL

nondigital NC, D_plus_AIN1, D_minus_AIN2, AIN3, AIN4, AIN5, AIN6, AIN7, AIN8
inputs SCL, CS_bar
outputs Dout, Int_Int_bar
bidirectional SDA
power  VCC, GND

disable SDA with SCL to "1"
set terminators to on
set load on groups Dout to no load


! ******************************************************************************
!  VECTORS
! ******************************************************************************

vector  Init
   drive SDA
   set   SCL                  to "1"
   set   SDA                  to "1"
end vector

vector  KEEP
   drive SDA
   set   SCL                  to "k"
   set   SDA                  to "k"
end vector

vector  Begin
   initialize                 to Keep
   drive SDA
   set   SCL                  to "1"
   set   SDA                  to "1"
end vector

vector  Start
   initialize                 to Keep
   drive SDA
   set   SCL                  to "1"
   set   SDA                  to "0"
end vector

vector  Stop
   initialize                 to Keep
   drive SDA
   set   SCL                  to "1"
   set   SDA                  to "1"
end vector

vector  SCL_0
   initialize                 to Keep
   drive SDA
   set   SCL                  to "0"
   set   SDA                  to "k"
end vector

vector  SCL_1
   initialize                 to Keep
   drive SDA
   set   SCL                  to "1"
   set   SDA                  to "k"
end vector

vector  Dd_0
   initialize                 to Keep
   drive SDA
   set   SCL                  to "k"
   set   SDA                  to "0"
end vector

vector  Dd_1
   initialize                 to Keep
   drive SDA
   set   SCL                  to "k"
   set   SDA                  to "1"
end vector

vector  SCL_0_D_X
   initialize                 to Keep
   receive  SDA
   set   SCL                  to "0"
   set   SDA                  to "x"
end vector

vector  SCL_1_D_X
   initialize                 to Keep
   receive  SDA
   set   SCL                  to "1"
   set   SDA                  to "x"
end vector

vector  ACK_S0
   initialize                 to Keep
   receive  SDA
   set   SCL                  to "1"
   set   SDA                  to "0"
end vector

vector  SCL_R_0
   initialize                 to Keep
   receive  SDA
   set   SCL                  to "0"
   set   SDA                  to "x"
end vector

vector  SCL_R_1
   initialize                 to Keep
   receive  SDA
   set   SCL                  to "1"
   set   SDA                  to "x"
end vector

vector  Dr_0
   initialize                 to Keep
   receive  SDA
   set   SCL                  to "k"
   set   SDA                  to "0"
end vector

vector  Dr_1
   initialize                 to Keep
   receive  SDA
   set   SCL                  to "k"
   set   SDA                  to "1"
end vector

vector  ACK_M0
   initialize                 to Keep
   drive SDA
   set   SCL                  to "k"
   set   SDA                  to "0"
end vector

vector  ACK_M1
   initialize                 to Keep
   receive  SDA
   set   SCL                  to "k"
   set   SDA                  to "1"
end vector

vector  RW_0
   initialize                 to Keep
   drive SDA
   set   SCL                  to "k"
   set   SDA                  to "0"
end vector

vector  RW_1
   initialize                 to Keep
   drive SDA
   set   SCL                  to "k"
   set   SDA                  to "1"
end vector

vector  SCL_1_D_0
   initialize                 to Keep
   drive SDA
   set   SCL                  to "1"
   set   SDA                  to "0"
end vector

! ******************************************************************************
!  SUBS
! ******************************************************************************


sub  IC_Address_000 (RW)
   execute  SCL_0
   execute  Dd_1     !1
   execute  SCL_1
   execute  SCL_0
   execute  Dd_0     !0
   execute  SCL_1
   execute  SCL_0
   execute  Dd_1     !0
   execute  SCL_1
   execute  SCL_0
   execute  Dd_1     !1
   execute  SCL_1
   execute  SCL_0
   execute  Dd_0     !0
   execute  SCL_1
   execute  SCL_0
   execute  Dd_0     !0
   execute  SCL_1
   execute  SCL_0
   execute  Dd_0     !0
   execute  SCL_1
   execute  SCL_0
   execute  RW       !Read = 1  Write = 0
   execute  SCL_1
   execute  SCL_0_D_X
   execute  SCL_1_D_X
   execute  Ack_S0         !ACK von Slave
   execute  SCL_0_D_X
end sub

sub  IC_Address_010 (RW)
   execute  SCL_0
   execute  Dd_1     !1
   execute  SCL_1
   execute  SCL_0
   execute  Dd_0     !0
   execute  SCL_1
   execute  SCL_0
   execute  Dd_0     !0
   execute  SCL_1
   execute  SCL_0
   execute  Dd_1     !1
   execute  SCL_1
   execute  SCL_0
   execute  Dd_0     !0
   execute  SCL_1
   execute  SCL_0
   execute  Dd_1     !1
   execute  SCL_1
   execute  SCL_0
   execute  Dd_0     !0
   execute  SCL_1
   execute  SCL_0
   execute  RW       !Read = 1  Write = 0
   execute  SCL_1
   execute  SCL_0_D_X
   execute  SCL_1_D_X
   execute  Ack_S0         !ACK von Slave
   execute  SCL_0_D_X
end sub

sub  Word_Address (D0,D1,D2,D3,D4,D5,D6,D7)
   execute  D0
   execute  SCL_1
   execute  SCL_0
   execute  D1
   execute  SCL_1
   execute  SCL_0
   execute  D2
   execute  SCL_1
   execute  SCL_0
   execute  D3
   execute  SCL_1
   execute  SCL_0
   execute  D4
   execute  SCL_1
   execute  SCL_0
   execute  D5
   execute  SCL_1
   execute  SCL_0
   execute  D6
   execute  SCL_1
   execute  SCL_0
   execute  D7
   execute  SCL_1
   execute  SCL_0_D_X
   execute  SCL_1_D_X
   execute  ACK_S0            !Ack von Slave
   execute  SCL_0_D_X
end sub

sub  Write_Data (D0,D1,D2,D3,D4,D5,D6,D7)
   execute  SCL_0_D_X     !1/3/00(mikef)
   execute  D0
   execute  SCL_1
   execute  SCL_0
   execute  D1
   execute  SCL_1
   execute  SCL_0
   execute  D2
   execute  SCL_1
   execute  SCL_0
   execute  D3
   execute  SCL_1
   execute  SCL_0
   execute  D4
   execute  SCL_1
   execute  SCL_0
   execute  D5
   execute  SCL_1
   execute  SCL_0
   execute  D6
   execute  SCL_1
   execute  SCL_0
   execute  D7
   execute  SCL_1
   execute  SCL_0_D_X
   execute  SCL_1_D_X
   execute  ACK_S0
   execute  SCL_0_D_X
   execute  Dd_0
   execute  SCL_0
   execute  SCL_1
end sub

sub  Read_Data (D0,D1,D2,D3,D4,D5,D6,D7)
   execute  SCL_R_1
   execute  D0        !D0
   execute  SCL_R_0
   execute  SCL_R_1
   execute  D1        !D1
   execute  SCL_R_0
   execute  SCL_R_1
   execute  D2        !D2
   execute  SCL_R_0
   execute  SCL_R_1
   execute  D3        !D3
   execute  SCL_R_0
   execute  SCL_R_1
   execute  D4        !D4
   execute  SCL_R_0
   execute  SCL_R_1
   execute  D5        !D5
   execute  SCL_R_0
   execute  SCL_R_1
   execute  D6        !D6
   execute  SCL_R_0
   execute  SCL_R_1
   execute  D7        !D7
   execute  SCL_0_D_X
   execute  ACK_M0            !Ack von Master
   execute  SCL_1_D_0
   execute  SCL_0_D_X
end sub


sub  Write_Data_No_Stop (D0,D1,D2,D3,D4,D5,D6,D7)
   execute  SCL_0_D_X     !1/3/00(mikef)
   execute  D0
   execute  SCL_1
   execute  SCL_0
   execute  D1
   execute  SCL_1
   execute  SCL_0
   execute  D2
   execute  SCL_1
   execute  SCL_0
   execute  D3
   execute  SCL_1
   execute  SCL_0
   execute  D4
   execute  SCL_1
   execute  SCL_0
   execute  D5
   execute  SCL_1
   execute  SCL_0
   execute  D6
   execute  SCL_1
   execute  SCL_0
   execute  D7
   execute  SCL_1
   execute  SCL_0_D_X
   execute  SCL_1_D_X
   execute  ACK_S0
   execute  SCL_0_D_X
!  execute  Dd_0
!  execute  SCL_0
!  execute  SCL_1
end sub

sub Reset
   repeat 10 times
     execute SCL_0
     execute SCL_1
     execute SCL_0
   end repeat
end sub

! ******************************************************************************
!  UNITS
! ******************************************************************************


unit  "Read ID Code"
   execute  Init
   execute  Begin   ! c=1 d=1
   call Reset
   execute  Start   ! c=1 d=0
   execute  Stop    ! c=1 d=1
   call  IC_Address_010 (RW_0)       !!Pin 11 floating
   call  Word_Address (Dd_0,Dd_1,Dd_0,Dd_0, Dd_1,Dd_1,Dd_0,Dd_1) !0x4D
   execute  Begin
   execute  Start
   call  IC_Address_010 (RW_1)
   call  Read_Data(Dr_0,Dr_0,Dr_0,Dr_0, Dr_0,Dr_0,Dr_1,Dr_0) !ID - 0x02
   call  Read_Data(Dr_0,Dr_1,Dr_0,Dr_0, Dr_0,Dr_0,Dr_0,Dr_1) !Manf ID - 0x41
   call  Read_Data(Dr_0,Dr_0,Dr_0,Dr_0, Dr_0,Dr_1,Dr_0,Dr_0) !Silicon Rev 0x04
   execute  Stop
end unit
!
!  End of test
!





